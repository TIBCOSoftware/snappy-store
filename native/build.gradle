/*
 * Copyright (c) 2017-2019 TIBCO Software Inc. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you
 * may not use this file except in compliance with the License. You
 * may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or
 * implied. See the License for the specific language governing
 * permissions and limitations under the License. See accompanying
 * LICENSE file.
 */
//plugins{
//  id "de.undercouch.download" version "4.1.1"
//}

buildscript {
  repositories {
    maven { url 'https://plugins.gradle.org/m2' }
  }
  dependencies {
    classpath 'de.undercouch:gradle-download-task:4.1.1'
  }
}
apply plugin: 'wrapper'
apply plugin: 'de.undercouch.download'

if ((rootProject.name.contains('native') && !rootProject.hasProperty('skipNative'))
    || rootProject.hasProperty('native')) {

  apply plugin: 'cpp'
  apply plugin: 'visual-studio'

  String osName = System.properties['os.name']
  osName = osName.substring(0, 3).toLowerCase()
  buildRoot = buildRoot.trim()
  if (!buildRoot.isEmpty()) {
    buildDir = new File(buildRoot, osName + '/' +  project.path.replace(':', '/'))
  } else {
    buildDir = 'build-artifacts/' + osName
  }

  String distDir = "${rootProject.projectDir}/dist"
  String thriftVersion = '0.14.1'
  String boostVersion = '1.76.0'
  String mpirVersion = '2.7.2'
  String opensslVersion = '1.1.1k'
  String googletestVersion = '1.10.0'

  String releasesDir = "https://github.com/TIBCOSoftware/snappy-thrift/releases/download/v${thriftVersion}"

  List<String> dependencies = []

  def getLinuxFlavour = { ByteArrayOutputStream out ->
    exec {
      executable 'lsb_release'
      args '-sir'
      standardOutput = out
    }
  }

  if (osName == 'lin') {
    // get the OS installation flavour
    String flavour = ''
    if (rootProject.hasProperty('linuxFlavour')) {
      flavour = linuxFlavour
    } else {
      def out = new ByteArrayOutputStream()
      getLinuxFlavour(out)
      flavour = out.toString().replaceAll('[ \r\n\f]', '').toLowerCase()
    }

    if (flavour.startsWith('ubuntu18') || flavour.startsWith('ubuntu19') ||
        flavour.startsWith('pop18') || flavour.startsWith('pop19') ||
        flavour.startsWith('linuxmint19') || flavour.startsWith('elementary5')) {
      dependencies += [ "thrift-${thriftVersion}-ubuntu18.04.tar.bz2",
                        "boost-${boostVersion}-ubuntu18.04.tar.bz2",
                        "googletest-${googletestVersion}-ubuntu18.04.tar.bz2" ]
    } else if (flavour.startsWith('ubuntu20') || flavour.startsWith('ubuntu21') ||
        flavour.startsWith('pop20') || flavour.startsWith('pop21') ||
        flavour.startsWith('linuxmint20') || flavour.startsWith('elementary6')) {
      dependencies += [ "thrift-${thriftVersion}-ubuntu20.04.tar.bz2",
                        "boost-${boostVersion}-ubuntu20.04.tar.bz2",
                        "googletest-${googletestVersion}-ubuntu20.04.tar.bz2" ]
    } else if (flavour.startsWith('archlinux') || flavour.startsWith('manjarolinux') ||
        flavour.startsWith('garuda')) {
      dependencies += [ "thrift-${thriftVersion}-arch21.05.tar.bz2",
                        "boost-${boostVersion}-arch21.05.tar.bz2",
                        "googletest-${googletestVersion}-arch21.05.tar.bz2" ]
    } else if (flavour.startsWith('redhat7') || flavour.startsWith('centos7')) {
      dependencies += [ "thrift-${thriftVersion}-rhel7.tar.bz2",
                        "boost-${boostVersion}-rhel7.tar.bz2",
                        "googletest-${googletestVersion}-rhel7.tar.bz2" ]
    } else if (flavour.startsWith('redhat8') || flavour.startsWith('fedora') ||
        flavour.startsWith('centos8')) {
      dependencies += [ "thrift-${thriftVersion}-rhel8.tar.bz2",
                        "boost-${boostVersion}-rhel8.tar.bz2",
                        "googletest-${googletestVersion}-rhel8.tar.bz2" ]
    } else {
      throw new GradleException("Unhandled linux flavour ${flavour}. Use -PlinuxFlavour to force one")
    }
  } else {
    dependencies += [ "thrift-${thriftVersion}-${osName}.tar.bz2",
                      "boost-${boostVersion}-${osName}.tar.bz2",
                      "googletest-${googletestVersion}-${osName}.tar.bz2" ]
  }
  if (osName == 'win') {
    dependencies += [ "mpir-${mpirVersion}-${osName}.tar.bz2",
                      "openssl-${opensslVersion}-${osName}.tar.bz2" ]
  }

  task downloadDependencies(type: de.undercouch.gradle.tasks.download.Download) {
    outputs.files   dependencies.collect { "${distDir}/${it}" }

    src                   dependencies.collect { "${releasesDir}/${it}" }
    dest                  distDir
    compress              false
    acceptAnyCertificate  true
    onlyIfNewer           true

    doFirst {
      mkdir(distDir)
    }
  }

  task extractDependencies {
    dependsOn downloadDependencies

    inputs.files dependencies.collect { [ "${distDir}/${it}" ] }.flatten()
    outputs.dirs dependencies.collect { [ "${distDir}/${it.replaceAll('-[^-]*.tar.bz2', '')}" ] }.flatten()

    doLast {
      inputs.files.each { tarball -> delete tarball.toString().replaceAll('-[^-]*.tar.bz2', '') }
      dependencies.each { tarball ->
        if (osName == 'win') {
          copy {
            from tarTree(resources.bzip2("${distDir}/${tarball}"))
            into distDir
          }
        } else {
          // gradle tarTree does not preserve symlinks (GRADLE-2844)
          exec {
            executable 'tar'
            args 'xjf', "${distDir}/${tarball}"
            workingDir = distDir
          }
        }
      }
    }
  }

  // C++ client
  model {
    buildTypes {
      debug
      release
    }
    platforms {
      x86 {
        architecture 'x86'
      }
      x64 {
        architecture 'x86_64'
      }
    }

    def boostLibs = [ 'boost_chrono', 'boost_date_time', 'boost_filesystem',
                      'boost_system', 'boost_thread' ]

    def getArch = { Platform platform ->
      return (platform == platforms.x64) ? "${osName}64" : "${osName}32"
    }
    def getArchDir = { String libName, String libVersion, String libDir,
        Platform platform ->
      return libVersion.isEmpty() ? "${libDir}/${getArch(platform)}"
          : "${libDir}/${libName}-${libVersion}/${getArch(platform)}"
    }
    def getBuildTypeLibDir = { String libDir, BuildType buildType ->
      if (buildType == buildTypes.debug) {
        String debugLibDir = "${libDir}/debug"
        if (file(debugLibDir).exists()) {
          return debugLibDir
        }
      }
      return libDir
    }
    def getLibPathForPlatform = { String libName, String distName, String libVersion,
        String libDir, boolean isStatic, boolean isBoost,
        BuildType buildType, Platform platform ->
      def os = platform.operatingSystem
      String archDir = getArchDir(distName, libVersion, libDir, platform)
      if (os.windows) {
        String extension = isStatic ? 'lib' : 'dll'
        if (isBoost) {
          String platformName = (platform == platforms.x64) ? '64' : '32'
          String suffix = "vc142-mt-x${platformName}-1_76"
          if (buildType == buildTypes.debug) suffix = "vc142-mt-gd-x${platformName}-1_76"
          if (isStatic) libName = "lib${libName}"
          return "${archDir}/lib/${libName}-${suffix}.${extension}"
        } else {
          String extDir = isStatic ? 'lib' : 'bin'
          String libLoc = getBuildTypeLibDir("${archDir}/${extDir}", buildType)
          String suffix = ''
          if (buildType == buildTypes.debug) suffix = 'd'
          String libFiled = "${libLoc}/${libName}${suffix}.${extension}"
          String libFile = "${libLoc}/lib${libName}.${extension}"
          if (file(libFiled).exists()) {
            return libFiled
          } else if (file(libFile).exists()) {
            return libFile
          } else {
            return "${libLoc}/${libName}.${extension}"
          }
        }
      } else if (isStatic) {
        return getBuildTypeLibDir("${archDir}/lib", buildType) + "/lib${libName}.a"
      } else if (os.macOsX) {
        return getBuildTypeLibDir("${archDir}/lib", buildType) + "/lib${libName}.dylib"
      } else {
        return getBuildTypeLibDir("${archDir}/lib", buildType) + "/lib${libName}.so"
      }
    }

    repositories {
      libs(PrebuiltLibraries) { libs ->
        boost {
          headers.srcDir "${distDir}/boost-${boostVersion}/include"
        }
        boostLibs.each { boostLib ->
          libs.create(boostLib) {
            binaries.withType(StaticLibraryBinary) {
              staticLibraryFile = file(getLibPathForPlatform(boostLib, 'boost',
                    boostVersion, distDir, true, true, buildType, targetPlatform))
            }
          }
        }
        thrift {
          binaries.withType(StaticLibraryBinary) {
            headers.srcDir getArchDir('thrift', thriftVersion, distDir, targetPlatform) + '/include'
            staticLibraryFile = file(getLibPathForPlatform('thrift', 'thrift',
                  thriftVersion, distDir, true, false, buildType, targetPlatform))
          }
        }
        openssl_crypto {
          binaries.withType(SharedLibraryBinary) {
            sharedLibraryFile = file(getLibPathForPlatform('libcrypto', 'openssl',
                  opensslVersion, distDir, false, false, buildType, targetPlatform))
            sharedLibraryLinkFile = file(getLibPathForPlatform('libcrypto', 'openssl',
                  opensslVersion, distDir, true, false, buildType, targetPlatform))
          }
          /*
          binaries.withType(StaticLibraryBinary) {
            staticLibraryFile = file(getLibPathForPlatform('libcrypto', 'openssl',
                  opensslVersion, distDir, true, false, buildType, targetPlatform))
          }
          */
        }
        openssl {
          binaries.withType(SharedLibraryBinary) {
            headers.srcDir getArchDir('openssl', opensslVersion, distDir, targetPlatform) + '/include'
            sharedLibraryFile = file(getLibPathForPlatform('libssl', 'openssl',
                  opensslVersion, distDir, false, false, buildType, targetPlatform))
            sharedLibraryLinkFile = file(getLibPathForPlatform('libssl', 'openssl',
                  opensslVersion, distDir, true, false, buildType, targetPlatform))
          }
          /*
          binaries.withType(StaticLibraryBinary) {
            staticLibraryFile = file(getLibPathForPlatform('libssl', 'openssl',
                  opensslVersion, distDir, true, false, buildType, targetPlatform))
          }
          */
        }
        mpir {
          binaries.withType(StaticLibraryBinary) {
            headers.srcDir getArchDir('mpir', mpirVersion, distDir, targetPlatform) + '/include'
            staticLibraryFile = file(getLibPathForPlatform('mpir', 'mpir',
                  mpirVersion, distDir, true, false, buildType, targetPlatform))
          }
        }
      }
    }
    components {
      snappyclient(NativeLibrarySpec) {
        targetPlatform 'x64'
        if (bothArch == '1') {
          targetPlatform 'x86'
        }
        sources {
          cpp.lib library: 'thrift', linkage: 'static'
          cpp.lib library: 'boost', linkage: 'api'
          boostLibs.each { boostLib ->
            cpp.lib library: boostLib, linkage: 'static'
          }
          if (osName == 'win') {
            cpp.lib library: 'mpir', linkage: 'static'
            cpp.lib library: 'openssl_crypto', linkage: 'shared'
            cpp.lib library: 'openssl', linkage: 'shared'
          }
        }
        // build only static libraries (not being shipped separately yet)
        binaries.withType(SharedLibraryBinarySpec) {
          buildable = false
        }
        binaries.all {
          // Define toolchain-specific compiler and linker options
          // TODO: precompiled header
          if (toolChain in Gcc) {
            cppCompiler.define 'PIC'
            cppCompiler.args '-Wall', '-Wno-unused-local-typedefs',
                             '-fPIC', '-std=c++11'
            if (buildType == buildTypes.debug) {
              cppCompiler.args '-g', '-O0'
            } else {
              cppCompiler.args '-g', '-O2'
            }

            linker.args '-lcrypto', '-lssl', '-lgmp', '-lpthread', '-lrt',
                        '-rdynamic', '-ldl', '-z', 'defs'
          } else if (toolChain in VisualCpp) {
            cppCompiler.define 'DLLBUILD'
            cppCompiler.args '/W3', '/FS', '/Zc:inline', '/WX', '/EHsc',
                             '/Fdsnappyclient.pdb', '/errorReport:prompt'
            if (buildType == buildTypes.debug) {
              cppCompiler.define '_DEBUG'
              cppCompiler.args '/Od', '/ZI', '/RTC1', '/MDd'
              linker.args '/DEBUG'
            } else {
              cppCompiler.args '/O2', '/GL', '/Oi', '/Zi', '/MD'
              linker.args '/LTCG'
            }
          }
        }
      }
    }
  }

  assemble.dependsOn extractDependencies

  // jar.enabled = false
  task product(dependsOn: assemble) {
    String productDir = "${buildDir}/snappyclient"

    doFirst {
      delete productDir
      file(productDir).mkdirs()
    }

    doLast {
      // when both 32-bit and 64-bit are built, then use arch specific dirs
      if (bothArch == '1') {
        // copy the release libraries
        copy {
          from "${buildDir}/libs/snappyclient/static/x64/release"
          into "${productDir}/${osName}64/lib"
        }
        copy {
          from "${buildDir}/libs/snappyclient/static/x86/release"
          into "${productDir}/${osName}32/lib"
        }
        // copy the debug libraries
        copy {
          from "${buildDir}/libs/snappyclient/static/x64/debug"
          into "${productDir}/${osName}64/lib/debug"
        }
        copy {
          from "${buildDir}/libs/snappyclient/static/x86/debug"
          into "${productDir}/${osName}32/lib/debug"
        }
      } else {
        // copy the release libraries
        if (file("${buildDir}/libs/snappyclient/static/release").exists()) {
          copy {
            from "${buildDir}/libs/snappyclient/static/release"
            into "${productDir}/${osName}64/lib"
          }
        } else {
          copy {
            from "${buildDir}/libs/snappyclient/static"
            into "${productDir}/${osName}64/lib"
          }
        }
        // copy the debug libraries
        copy {
          from "${buildDir}/libs/snappyclient/static/debug"
          into "${productDir}/${osName}64/lib/debug"
        }
      }
      // copy the public headers
      copy {
        from "${projectDir}/src/snappyclient/headers"
        into "${productDir}/include"
      }

      if (rootProject.hasProperty('copyTo')) {
        copy {
          from productDir
          into copyTo
        }
      }
    }
  }
}
